// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Write
    DMux(in=load, sel=address[14], a=loadd, b=loadsk);
    DMux(in=loadsk, sel=address[13], a=loads, b=loadk);

    // Data Memory
    RAM16K(in=in, load=loadd, address=address[0..13], out=dout);
    // Screen
    Screen(in=in, load=loads, address=address[0..12], out=sout);
    // Keyboard
    Keyboard(out=kout);


    // Read

    // k and false -> kfout
    Or8Way(in=address[0..7], out=nz0);
    Or(a=nz0, b=address[8], out=nz1);
    Or(a=nz1, b=address[9], out=nz2);
    Or(a=nz2, b=address[10], out=nz3);
    Or(a=nz3, b=address[11], out=nz4);
    Or(a=nz4, b=address[12], out=nz);

    Mux16(a=kout, b=false, sel=nz, out=kfout);
    // kfout and sout -> skout
    Mux16(a=sout, b=kfout, sel=address[13], out=skout);
    // dout and skout -> out
    Mux16(a=dout, b=skout, sel=address[14], out=out);
}







